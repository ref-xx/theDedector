' Mayin Tarayici © 1996 Arda Erdikmen and APM Entertainment  

Dim HI$(4),HI(4)
If Exist("Appleseeds") Then Gosub HISOK Else Gosub HIDF
'Screen Close 0
Screen Open 1,640,256,4,Hires : Rem  Screen Hide 1 
Curs Off : Flash Off : Cls 0
Screen Open 2,640,256,4,Hires : Screen Hide 2
Colour 3,$A00
Curs Off : Flash Off : Cls 0
Screen Open 0,640,256,16,Hires
Get Bob Palette 
Palette $FFF
Flash Off : Curs Off 
Cls 0
Colour 18,$999
Colour 19,$333
Colour 17,$CCC
STAR:
'X=30
'Y=30
'M=10
Gosub MAIN_MENU1
Get Bob Palette 
Palette $FFF
Flash Off : Curs Off 
Cls 0
Colour 18,$999
Colour 19,$333
Colour 17,$CCC
' The field: 

Rem game 
GEN_FIELD:
'
' x= Genislik
' y= Yukseklik 
' M= Mayin sayisi
'  
SYCHEAT=0 : SYSOP=0
Screen 2 : Cls 0 : Screen To Front 2
Paper 0 : Pen 2
Ink 3
Centre At(,10)+"Lutfen bekleyin...Tarla olusuyor..."
Plot 50,180
Plot(X*20)+50,180
Screen Show 2

MC=0
Screen 1
'Screen To Front 1 
Wait Vbl 
Cls 0

Screen Hide 1

For MX=1 To X
   For MY=1 To Y
      A=Rnd((X*Y)/M)
      If A=1 and MC<M
         Bob 1,MX*16,MY*8,18
         Put Bob 1
         Wait Vbl 
         Inc MC
      End If 
      'If MC>M/2 Then Goto DEVAM 
Next : Next 
'Goto GAM
DEVAM:
For MX=X To 1 Step -1
   For MY=1 To Y
      A=Rnd((X*Y)/M)
      R=Point(MX*16,MY*8)
      If A=1 and MC<M and R<1
         Bob 1,MX*16,MY*8,18
         Put Bob 1
         Wait Vbl 
         Inc MC
      End If 
Next : Next 
GAM:
Bob 1,1000,1000,18
Bob Off 1

Screen 0
'Screen To Front 0 
Cls 0
Wait Vbl 
Unpack 7 To 0
Screen Hide 0
Wait Vbl 

Ink 8
Bar 8,4 To(X+1)*16+8,(Y+1)*8+4
Ink 0
Box 12,6 To(X+1)*16+4,(Y+1)*8+2

For MX=1 To X
   Screen 2 : Draw 50,180 To 50+(MX*20),180 : Screen 0
   For MY=1 To Y
      Bob 1,MX*16,MY*8,13
      Put Bob 1
      Wait Vbl 
   Next 
Next 

'Print At(60,10);MC

Screen Show 0
Wait Vbl 
Wait Vbl 
XLIM=MX*16
YLIM=MY*8
'Bob 1,1000,1000,11
Screen 2 : Cls 0
Wait 10
Screen To Front 0


PLETTO:
Screen 0
BAS=MC-1 : BYS=0
Timer=0 : Pen 7 : Paper 5
GAME:
If BYS=MC-1 and BAS=0 : Bell 17 : Wait 5 : Bell 45 : Wait 3 : Bell 70 : Wait 20 : Goto BITTI : End If 
If Mouse Key<>0 Then Goto GAME
Do 
   SA=((Timer/50)/60)/60
   DK=(Timer/50)/60
   SN=(Timer/50)-(DK*60)
   Print At(64,4);"  MAYINLAR:";MC-BYS-1;" ";At(64,2);" Sure:";SA;":";DK;":";SN;" "
   If SYSOP=1 Then Print At(65,4);"*" : If SYCHEAT=1 Then Print At(64,4);"*"
   MK=Mouse Key
   If MK=1 or MK=2 Then Exit 
   If Key State(82) and SN<3 Then SYSOP=1
   If Key State(86) and SYSOP=1 Then Gosub ACIKLA
   If Key State(88) and SYSOP=1 Then Gosub KAPAT
   If Key State(81) and SYSOP=1 Then SYCHEAT=1
Loop 
If ZIL=1 Then Bell 90
XM=X Screen(X Mouse) : YM=Y Screen(Y Mouse)
If XM>XLIM or YM>YLIM or XM<16 or YM<8 Then Goto GAME
If MK=1
   Screen 1
   R=Point(XM,YM)
   If R=3 : Goto GAME : End If 
   If R<>0
      If SYCHEAT=1
         Shoot : Goto GAME
      Else 
      Goto BUMLADI : End If 
   End If 
   Gosub ETRAFBAK
   Gosub NUMARAKOY
End If 
If MK=2 Then Goto BAYRAKLA

Goto GAME
BUMLADI:
Screen 0 : Boom 
Gosub ACIKLA
Wait Vbl 
LPO:
Do 
   If Mouse Key<>0 Then Exit 
Loop 
Bob Off 
Screen 1
Wait Vbl 
Goto STAR

End 
ETRAFBAK:
Screen 1
S=0
B=Point(XM,YM-8) : If B>0 Then Inc S : B=0
B=Point(XM,YM+8) : If B>0 Then Inc S : B=0
B=Point(XM-16,YM+8) : If B>0 Then Inc S : B=0
B=Point(XM-16,YM-8) : If B>0 Then Inc S : B=0
B=Point(XM+16,YM+8) : If B>0 Then Inc S : B=0
B=Point(XM+16,YM-8) : If B>0 Then Inc S : B=0
B=Point(XM-16,YM) : If B>0 Then Inc S : B=0
B=Point(XM+16,YM) : If B>0 Then Inc S : B=0
Return 
NUMARAKOY:
Screen 0
If S=0 Then S=9
'Print At(60,20);S 
RX=XM/16
RY=YM/8
Bob 1,RX*16,RY*8,S
Put Bob 1
Wait Vbl 
'Screen 1
'Wait Vbl  
'Bob 2,RX*16,RY*8,20 
'Put Bob 2 
'Wait Vbl  
Return 

BAYRAKLA:
'Bob Off 
'Screen Show 2 
'Screen To Front 2 
Screen 2
Wait Vbl 
B=Point(XM,YM)
Screen 1
Wait Vbl 
B2=Point(XM,YM)
Screen 2
Wait Vbl 
If B>1 Then Goto SKP
If B2<>0 Then Dec BAS
SKP:
If B=2 and B2<>0 Then Inc BAS
If B<2 Then S=19 : Inc BYS : S1=11
If B=2 Then S=20 : Dec BYS : S1=12
If B=3 Then S=18 : S1=13
RX=XM/16
RY=YM/8
Bob 2,RX*16,RY*8,S
Put Bob 2
Wait Vbl 

'Bob Off 
Screen 0 : Rem Screen To Front 0
Wait Vbl 
Bob 1,RX*16,RY*8,S1
Put Bob 1
Wait Vbl 
Goto GAME

ACIKLA:
For MX=1 To X
   For MY=1 To Y
      Screen 1
      A=Point(MX*16,MY*8)
      
      Screen 0
      If A>0
         Bob 1,MX*16,MY*8,10
         Put Bob 1
         Wait Vbl 
         
      End If 
Next : Next 
Return 



MAIN_MENU1:
Screen 2 : Cls 0
Screen 0
Screen To Front 0
Wait Vbl 
Unpack 6 To 0
Colour 18,$999
Colour 19,$333
Colour 17,$CCC
Wait Vbl 
Pen 0 : Paper 1
Print At(30,2);"V 1.1t"
For REP=0 To 4
   Print At(43,REP+17);HI$(REP)
Next 
MAIN_MENU:
Do 
   If Mouse Key=1 Then Exit 
Loop 
MX=X Screen(X Mouse)
MY=Y Screen(Y Mouse)
If MX>-1 and MY>-1 and MX<8 and MY<20 Then Goto HISC
If MY<55 Then Goto MAIN_MENU
If MX>320 Then Goto UPMEN
DWNMEN:
If MY>55 and MY<91 Then SC=1
If MY>91 and MY<130 Then SC=2
If MY>130 and MY<165 Then SC=3
If MY>165 and MY<203 Then SC=4
If MY>203 Then SC=5
Goto PRESET
UPMEN:
If MY>55 and MY<91 Then SC=10 : Goto OZEL
'If MY>130 and MY<165 and MX>486 Then SC=7 
'If MY>130 and MY<165 and MX<86 Then SC=8
If MY>203
   If ZIL=1
      ZIL=0 : For BL=50 To 10 Step -1 : Bell BL : Wait 1 : Next 
      Goto MAIN_MENU
   End If 
   If ZIL=0
      ZIL=1 : For BL=10 To 50 Step 1 : Bell BL : Wait 1 : Next 
      
   End If 
End If 
Goto MAIN_MENU
PRESET:
Restore 1
For K=1 To SC
   Read X,Y,M
Next 
Return 
1 Data 8,8,10,16,16,40,30,16,89,30,30,120,18,14,55
OZEL:
Cls 0 : Pen 3 : Paper 0
Input "X (uzunluk) 5-30 arasi:";X
If X>30 or X<5 Then Goto OZEL
2 Input "y (yukseklik) 5-30 arasi:";Y
If X>30 or X<5 Then Goto 2
3 Print "Mayin sayisi (en cok ";(X*Y)-(((X*Y)/10)*5);" adet):"
Input M
If M>(X*Y)-(((X*Y)/10)*5) Then Goto 3
Return 

BITTI:
REC=Timer/50
Gosub ACIKLA
Print At(65,20);"Tebrikler!"
S=SC
If SC=10 Then Goto LPO
If S=1 and REC<HI(0) Then Gosub INIT
If S=2 and REC<HI(1) Then Gosub INIT
If S=3 and REC<HI(2) Then Gosub INIT
If S=4 and REC<HI(3) Then Gosub INIT
If S=5 and REC<HI(4) Then Gosub INIT

Goto LPO
End 
KAPAT:
For MX=1 To X
   For MY=1 To Y
      Bob 1,MX*16,MY*8,13
      Put Bob 1
      Wait Vbl 
Next : Next 
Screen 2 : Cls 0
Goto PLETTO

HISC:
Open Out 1,"AppleSeeds"
For X=0 To 4
   Print #1,HI$(X)
Next 
Close 1
System 

HISOK:
Open In 1,"appleseeds"
For X=0 To 4
   Input #1,HI$(X)
Next 
Close 1
ANL:
For X=0 To 4
   HF1=Val(Mid$(HI$(X),27,2))
   HF2=Val(Mid$(HI$(X),30,2))
   HF3=Val(Mid$(HI$(X),33,2))
   HF0=(HF1*60*60)+(HF2*60)+HF3
   HI(X)=HF0
Next X
Return 

HIDF:
HI$(0)="Max Sern        ... Kolay 00:05:00"
HI$(1)="Zeuss           ... Orta  00:20:00"
HI$(2)="Aspirin         ... ileri 00:50:00"
HI$(3)="Arda Oncu       ... Zor   02:00:00"
HI$(4)="APM © 1996      ... Svis  00:15:00"
Gosub ANL
Return 

INIT:
Screen 0
SA$=Right$(("00"+(Str$(SA)))-" ",2)
DK$=Right$(("00"+(Str$(DK)))-" ",2)
SN$=Right$(("00"+(Str$(SN)))-" ",2)
REC$=SA$+":"+DK$+":"+SN$
Paper 0
Cls 0
Screen To Front 0
Centre At(,10)+" En baba mayin tarayicilar kulubu!!!"
Print : Print 
Print "         En son taramis oldugunuz tarlanin buyuklugune oranla"
Print " birhayli hizli sekilde mayinlari cikartmaniz biz 'Tum zaman-"
Print " larin en baba mayin tarayicilari' kulubunu cok etkiledi.Sizi"
Print " kurulusumuzda gormekten gurur duyariz.Lutfen adinizi yazin:"
Print : Print 
Input " isim soyad:";I$
I$=I$+Space$(20)
Restore 10
For I=1 To S
   Read L$
Next 
HI$(S-1)=Left$(I$,16)+"..."+L$+REC$
Return 


10 Data " Kolay "," Orta  "," ileri "," Zor   "," Svis  "














